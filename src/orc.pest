// No whitespace allowed between digits
WHITESPACE        = _{ " " | "\n" | "\r" | "\t" }
COMMENT           = _{ SingleLineComment | MultiLineComment }
SingleLineComment =  { "//" ~ (!"\n" ~ ANY)* }
MultiLineComment  =  { "/*" ~ (!"*/" ~ ANY)* ~ "*/" }

integer_literal = @{ ASCII_NONZERO_DIGIT ~ ASCII_DIGIT* }
float_literal   = @{ ASCII_NONZERO_DIGIT ~ ASCII_DIGIT* ~ "." ~ ASCII_DIGIT* }
variable   = @{ ("_" | ASCII_ALPHA) ~ ("_" | ASCII_ALPHANUMERIC)* }

unary_minus =  { "-" }
primary     = _{ float_literal | integer_literal | variable | "(" ~ expr ~ ")" }
atom        = _{ unary_minus? ~ primary }

bin_op   = _{ add | subtract | multiply | divide | modulo }
add      =  { "+" }
subtract =  { "-" }
multiply =  { "*" }
divide   =  { "/" }
modulo   =  { "%" }

type = { variable ~ type_block? }
type_block = { "<" ~ type ~ ("," ~ type)* ~ ">" }
type_annotation = { ":" ~ type }

expr = { exp_block | bin_exp | "" }
bin_exp = {atom ~ (bin_op ~ atom)*}

exp_block = { "{" ~ statement* ~ expr ~ "}" }

statement = { (let_statement | expr) ~ ";" }
let_statement = { "let" ~ variable ~ type_annotation? ~ ("=" ~ expr)?}

main = _{ SOI ~ expr ~ EOI }
